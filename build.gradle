buildscript {
	ext {
		springBootVersion = '2.0.3.RELEASE'
		camelVersion = '2.18.1'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'

checkstyle {
	ignoreFailures = false
	toolVersion "8.11"
}
findbugs {
	ignoreFailures = false
}
pmd {
	ignoreFailures = false
}
gradle.taskGraph.beforeTask { task ->
	if (task.name == "checkstyleTest") {
		task.deleteAllActions()
	}
}
tasks.withType(FindBugs) {
	excludeFilter = file('config/findbugs/exclude-filter.xml')
	reports {
		xml.enabled false
		html.enabled true
	}
}
task copyLombok(type: Copy) {
	outputs.upToDateWhen { false }
	into "${buildDir}/libs"
	include "lombok*.jar"
	from configurations.compileOnly
}

build.dependsOn(copyLombok)

group = 'uk.gov.digital.ho.hocs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	jcenter()
}

dependencies {

	compile("org.springframework.boot:spring-boot-starter-web") {
		exclude module: "spring-boot-starter-tomcat"
	}
	
    compile('org.springframework.boot:spring-boot-starter-jetty')
	compile('net.logstash.logback:logstash-logback-encoder:5.1')
	compile('org.springframework.boot:spring-boot-starter-json')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.flywaydb:flyway-core')
	compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'
	compile group: 'org.apache.camel', name: 'camel-spring-boot', version: camelVersion
	compile group: 'org.apache.camel', name: 'camel-jackson', version: camelVersion
	compile group: 'org.apache.camel', name: 'camel-aws', version: camelVersion
	compile group: 'org.apache.camel', name: 'camel-gson', version: camelVersion
	compile group: 'org.apache.camel', name: 'camel-rabbitmq', version: camelVersion

	compile('org.apache.commons:commons-csv:1.5')
	compile('uk.gov.service.notify:notifications-java-client:3.9.1-RELEASE')

	runtime('org.postgresql:postgresql')

	compileOnly('org.projectlombok:lombok')

	testCompile group: 'org.apache.camel', name: 'camel-test-spring', version: camelVersion
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('io.projectreactor:reactor-test')
	testCompile('org.assertj:assertj-core')
}
gradle.startParameter.continueOnFailure = true
